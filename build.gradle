plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
}

group = 'com.toolrental.demo'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

jacoco {
    toolVersion = "0.8.9"
}

jacocoTestReport {
    dependsOn test  // Ensure tests are run before generating the report

    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: [
                            'com/toolrental/demo/dto/**',
                    ])
                })
        )
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-core'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'com.h2database:h2'
}

tasks.named('bootRun') {
    systemProperty 'spring.profiles.active', System.getProperty('APP_PROFILE')

    standardInput = System.in;
}

tasks.named('test') {
    useJUnitPlatform()

    systemProperty 'spring.profiles.active', System.getProperty('APP_PROFILE')

    testLogging {
        showStandardStreams = true
        events 'passed', 'skipped', 'failed'
    }

    finalizedBy jacocoTestReport  // Automatically generate code coverage report after running tests
}
